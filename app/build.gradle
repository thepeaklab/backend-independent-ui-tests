apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    testOptions {
        unitTests{
            all {
                testLogging {
                    outputs.upToDateWhen {false}
                    events "passed", "failed", "standardError"
                    showCauses true
                    showExceptions true
                }
            }
            returnDefaultValues = true
        }
        animationsDisabled = true
    }

    dataBinding {
        enabled true
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.thepeaklab.backendindependentuitests"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.thepeaklab.backendindependentuitests.core.MockTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}


configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dependencies {

    kapt "com.google.dagger:dagger-compiler:2.20"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"
    implementation 'com.google.android.material:material:1.0.0'
    implementation "com.google.dagger:dagger:2.20"
    implementation "com.google.dagger:dagger-android-support:2.12"
    kapt "com.google.dagger:dagger-android-processor:2.12"
    implementation 'com.amitshekhar.android:android-networking:1.0.2'
    implementation 'com.amitshekhar.android:jackson-android-networking:1.0.2'
    ktlint "com.github.shyiko:ktlint:0.30.0"

    // unit tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.dagger:dagger:2.20'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
    testImplementation "org.json:json:20140107"
    testImplementation 'android.arch.core:core-testing:1.1.1'

    // ui tests
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'com.google.dagger:dagger:2.20'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1'
    androidTestImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'android.arch.core:core-testing:1.1.1'
}
